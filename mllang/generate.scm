(define gen-file (open-output-file "generated.scm"))

(define (prepare-gen-file)
  (let ((lib-funcs-file (open-input-file "genbase.sm")))
    (while (not (eof-object? (peek-char lib-funcs-file)))
           (display (read-char lib-funcs-file) gen-file))))

(prepare-gen-file)

(define (to-gen-file text)
  (display text gen-file)
  (newline gen-file))

(define (string-op->symbol x stack)
  (cond ((equal? x "!") (list (cons 'not stack)))
        (else           (cons (string->symbol x) stack))))

(define (calc-rpn xs)
  (define (helper stack xs)
    ;(print 'calc-rpn xs stack)
    (if (null? xs)
        (car stack)
        (let ((x (car xs))
              (s (cdr xs)))
          (cond ((number? x) (helper (cons x stack) s))
                ((is-op? x)  (helper (cons `(,(string->symbol x) ,(cadr stack) ,(car stack))
                                           (cddr stack))
                                     s))
                ((is-uop? x) (helper (cons `(,(string->symbol x) ,(car stack))
                                           (cdr stack))
                                     s))
                ((list? x)   (helper (cons (func-apply x) stack) s))
                ((string? x) (helper (string-op->symbol x stack) s))
                ((x-in-xs? x #t #f) (helper (cons x stack) s))
                (else        (helper stack s))))))
  (helper '() xs))

(define (is-variable types)
  (and (eq? (length types) 1)
       (zero? (length (get-args-names-from-type (car types))))))

(define (generate-let-var name exprs inner)
  `(letrec ((,name ,(calc-rpn (car exprs))))
     ,inner))

(define (generate-func-types types)
  `(map-cond ,(map (lambda (type)
                     (let* ((lambda-and-let (make-var-lists type))
                            (lambda-list (car lambda-and-let))
                            (lambda-let  (cdr lambda-and-let))
                            (hash-args (get-args-for-check ':args type)))
                       ;(print 'generate-def type)
                       `((and (,(get-args-num-from-type type) (length :args))
                              (hash ',(get-args-check-from-type type) ,hash-args)
                              (,(get-similar-from-type type) :args))
                         (apply (lambda ,lambda-list
                                  ,(if (not-null? lambda-let)
                                       `(let ,(car lambda-let)
                                          ,(generate-let (get-defs-from-type type)
                                                         type))
                                       (generate-let (get-defs-from-type type)
                                                     type)))
                                :args))))
                   types)))

(define (generate-let-func name types inner)
  `(letrec ((,name (lambda :args ,(generate-func-types types))))
     ,inner))

(define (generate-let defs type)
  (if (null? defs)
      (cons 'begin
            (map generate-expr (get-exprs-from-type type)))
      (let* ((def (car defs))
             (name (string->symbol (car def)))
             (types (cdr def)))
        (if (is-variable types)
            (generate-let-var name
                              (get-exprs-from-type (car types))
                              (generate-let (cdr defs) type))
            (generate-let-func name types (generate-let (cdr defs) type))))))

(define (generate-expr expr)
  (calc-rpn expr))

(define (check-expr-or-func type)
  ((eval-i (get-args-num-from-type type)) 0))

(define (generate-def def)
  (let* ((name (string->symbol (car def)))
         (types (cdr def)))
    (to-gen-file (if (:and-fold (map check-expr-or-func types))
                     (let ((type (car (reverse types))))
                       `(define ,name
                                ,(generate-let (get-defs-from-type type)
                                               type)))
                     `(define (,name . :args)
                        ,(generate-func-types types))))))

(define (generate-defs defs)
  (map generate-def defs))

(define (calc-expr expr)
  (to-gen-file (generate-expr (list expr))))

(define (calc-exprs exprs)
  (map calc-expr exprs))

(define (generate model)
  (let* ((defs (car model))
         (exprs (cadr model)))
    (generate-defs defs)
    (calc-exprs exprs)
    (close-output-port gen-file)))
